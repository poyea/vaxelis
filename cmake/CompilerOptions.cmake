include(CheckIPOSupported)

function(cpp_package_boilerplate_apply_target_options target_name)
    target_compile_features(${target_name} PUBLIC cxx_std_${CMAKE_CXX_STANDARD})

    if(MSVC)
        target_compile_options(${target_name} PRIVATE /W4 /WX /permissive- /Zc:__cplusplus)
    else()
        target_compile_options(
            ${target_name}
            PRIVATE
                -Wall
                -Wextra
                -Wpedantic
                -Wconversion
                -Wsign-conversion
                -Wshadow
                -Werror)
    endif()

    set_target_properties(
        ${target_name}
        PROPERTIES
            CXX_VISIBILITY_PRESET hidden
            VISIBILITY_INLINES_HIDDEN YES
            POSITION_INDEPENDENT_CODE ON)

    if(CPP_PACKAGE_BOILERPLATE_ENABLE_IPO AND CMAKE_BUILD_TYPE STREQUAL "Release")
        check_ipo_supported(RESULT ipo_supported OUTPUT ipo_error)
        if(ipo_supported)
            set_property(TARGET ${target_name} PROPERTY INTERPROCEDURAL_OPTIMIZATION TRUE)
        else()
            message(WARNING "IPO/LTO is not supported: ${ipo_error}")
        endif()
    endif()

    if(CPP_PACKAGE_BOILERPLATE_ENABLE_SANITIZERS AND CMAKE_BUILD_TYPE STREQUAL "Debug")
        if(NOT MSVC)
            target_compile_options(${target_name} PRIVATE -fsanitize=address,undefined -fno-omit-frame-pointer)
            target_link_options(${target_name} PRIVATE -fsanitize=address,undefined)
        endif()
    endif()
endfunction()
